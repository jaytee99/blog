# SNOBOL4, Part 4

Now let's look at the interesting part!

## Pattern Matching

```
            PATTERN = 'EE'
LOOP  WORD = INPUT  : F(END)
      WORD PATTERN : F(LOOP)
      OUTPUT = WORD : S(LOOP)
END
```
The simplest pattern is a string, which is set up in line 1. Line 2 then reads from the input, assigning what's read to `WORD`. The pattern match occurs in line 3, where `WORD` and `PATTERN` are separated by whitespace; if the pattern matches, execution falls through to the next statement which outputs the word and goes back for the next one. If the match fails, we go back to read the next word, and this continues until there's no more input, at which point we branch to the end.

This tiny program makes you realize why structured programming is a Good Thing, because managing the flow in a complex program becomes non-trivial rather quickly.

Patterns can also be constructed, like this:

```
    ROW = 'K'
    NO = 20

    'K24' ROW NO + 4
```

The string `K24` is matched against a pattern formed by concatenating `K` with `NO + 4`, so it matches.

You can also change the part that you matched, so that if we change the third line to this:

```
     WORD PATTERN = 'OO' : F(LOOP)
```

Then the word `SEEN` will be matched, and changed to `SOON`.  You can delete the matched substring by putting nothing after the `=`.

Patterns support alternation as well as concatenation, so `'COMPUTER' | 'PROGRAM'` will match 'COMPUTER' or 'PROGRAM', and you can build on previous patterns, like so:

```
    KEYWORD = 'COMPUTER' | 'PROGRAM'
    KEYWORD = KEYWORD | 'ALGORITHM'
```

Concatenation has a higher precedence than alternation, so `'FIXED' | 'FLOAT' 'DECIMAL'` will match `'FIXED'` or `'FLOATDECIMAL'`.

## Conditional Value Assignment

You can associate a variable with a pattern, such that when the pattern matches, the variable is assigned the substring that was matched, e.g.

```
    BASE = ('HEX' | 'DEC') . B1
```

`BASE` is a pattern that will match 'HEX' or 'DEC', and when it does, `B1` will be set to whichever one it matched. And since input and output are done using the pseudo-variables `INPUT` and `OUTPUT`, so specifying `. OUTPUT` will print the substring whenever it is matched.

## Indirect Reference

The unary `$` operator provides a way to access variables indirectly, like this:

```
    MONTH = 'APRIL'
    $MONTH = 'CRUEL'
```

This is the same as `APRIL = 'CRUEL'`. Here's a more complicated example:

```
    N = N + 1  : ($('PHASE' N))
```

If `N` is 5, control is transferred to the statement labelled `PHASE5`. This could make static code analysis quite challenging! (As an aside, Fortran once had a construct like this, called the 'computed goto', which sensibly disappeared from later versions.)

Next up, functions and data types...
